plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id 'maven-publish'
    id 'net.darkhax.curseforgegradle' version '1.0.7'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = "${mod_name}-fabric-${minecraft_version}"

apply from: 'gradle/property_loader.gradle'
apply from: 'gradle/build_number.gradle'
apply from: 'gradle/git_changelog.gradle'

repositories {
}

dependencies {

	minecraft "com.mojang:minecraft:${project.minecraft_version}"
    
    mappings loom.layered() {
        officialMojangMappings()
    }
    
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

loom {

	accessWidenerPath = file('src/main/resources/ingredient-extension-api.accesswidener')
}

processResources {

	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 16
}

java {
	withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.archivesBaseName
            from(components.java)
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {

    description = 'Publishes the build to CurseForge.'

    apiToken = System.getenv("CURSEFORGE_API_TOKEN")

    def mainFile = upload(557735, file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar"))
    mainFile.changelog = project.ext.mod_changelog
    mainFile.releaseType = 'release'
    def sourcesFile = mainFile.withAdditionalFile(sourcesJar)
    def javadocFile = mainFile.withAdditionalFile(javadocJar)
}